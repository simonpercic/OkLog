buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$gradleBintrayVersion"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$androidGradleMavenVersion"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'si.kamino.soter'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
    }

    lintOptions {
        checkReleaseBuilds true
        textReport true
        warningsAsErrors true
        lintConfig file('../config/check/lint/lint.xml')
    }

    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // 10 minutes
        installOptions "-d", "-t"
    }

    dexOptions {
        if (System.getenv()["CIRCLECI"] as boolean) {
            javaMaxHeapSize "2048M"
            preDexLibraries false
        }
    }
}

ext.okhttpVersion = '2.7.5'

dependencies {
    compile project(':oklog-core')

    provided "com.squareup.okhttp:okhttp:$okhttpVersion"

    // Unit test dependencies
    testCompile project(':oklog-core-test')
    testCompile "com.squareup.okhttp:mockwebserver:$okhttpVersion"

    // Static code rules
    checkstyleRules files('../config/soter/checkstyle-0.3.xml')
    findbugsRules files('../config/soter/findbugs-0.1.xml')
    pmdRules files('../config/soter/pmd-0.1.xml')
}

soter {
    checkstyle {
        enabled true
    }

    findbugs {
        enabled true
        reportLevel 'high'
    }

    pmd {
        enabled true
    }
}

apply from: '../gradle-publish/android/jcenterupload.gradle'
